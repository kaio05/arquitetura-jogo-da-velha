// OBSERVACOES GERAIS
// ==================
//
// REGISTRADORES DE 1 A 9 CORRESPONDEM AS CELULAS
// 0 = vazio, X = 1, O = -1
//
// @16 (@TURN) -> JOGADOR ATUAL
// @17 (@TURNSX) -> QUANTOS TURNOS X JOGOU
// @18 (@WIN) -> VENCEDOR (0 = EMPATE)
//
// QUANDO O PONTEIRO OSCILAR EM @24576 (KBD), O PROGRAMA
// ESPERARA UMA JOGADA

(SETUP)
@R0
M=0
@R1
M=0
@R2
M=0
@R3
M=0
@R4
M=0
@R5
M=0
@R6
M=0
@R7
M=0
@R8
M=0
@R9
M=0
@TURN
M=1
@TURNSX
M=0
@WIN
M=0
@KBD
M=0

(XTURN)
@TURN
M=1
@TURNSX
M=M+1
@READ
0;JMP

(OTURN)
@TURN
M=-1
@READ
0;JMP

(READ)
@KBD // SE O VALOR LIDO FOR < 1 E > 9, LEIA DE NOVO
D=M
@49
D=D-A
@READ
D;JLT
@49
D=D+A
@57
D=D-A
@READ
D;JGT

@57 // SER O VALOR FOR VALIDO
D=D+A
@48
D=D-A
@R0 // SALVE O VALOR *NUMERICO* DA LEITURA
M=D
@WRITE
0;JMP

(WRITE)
@R0 // VA PARA A POSICAO SELECIONADA DO "TABULEIRO"
A=M
D=M
@PROCEEDWRITE // SE ESTIVER VAZIA, GRAVE 1 OU -1
D;JEQ
@READ   // SENAO, LEIA OUTRA POSICAO
0;JMP

(PROCEEDWRITE)
@TURN
D=M
M=-M    // TROQUE O TURNO
@R0
A=M
M=D // GRAVA 1 OU -1 NA POSICAO SELECIONADA
@TURNSX // SE X JA JOGOU 3 VEZES, VERIFIQUE POR UMA VITORIA
D=M
@2
D=D-A
@FINDRESULT
D;JGT // SENAO, VA PARA A PROXIMA JOGADA

(NEXTTURN)
@TURN
D=M
@XTURN
D;JGT
@OTURN
D;JLT

// CONFIGURACOES DE VITORIA
// 1, 2, 3
// 4, 5, 6
// 7, 8, 9

// 1, 4, 7
// 2, 5, 8
// 3, 6, 9

// 1, 5, 9
// 3, 5, 7

(FINDRESULT)
@R1 // PRIMEIRA LINHA
D=M
@R2
D=D+M
@R3
D=D+M
@3
D=D-A
@XWINS
D;JEQ
@6
D=D+A
@OWINS
D;JEQ

@R4 // SEGUNDA LINHA
D=M
@R5
D=D+M
@R6
D=D+M
@3
D=D-A
@XWINS
D;JEQ
@6
D=D+A
@OWINS
D;JEQ

@R7 // TERCEIRA LINHA
D=M
@R8
D=D+M
@R9
D=D+M
@3
D=D-A
@XWINS
D;JEQ
@6
D=D+A
@OWINS
D;JEQ

@R1 // PRIMEIRA COLUNA
D=M
@R4
D=D+M
@R7
D=D+M
@3
D=D-A
@XWINS
D;JEQ
@6
D=D+A
@OWINS
D;JEQ

@R2 // SEGUNDA COLUNA
D=M
@R5
D=D+M
@R8
D=D+M
@3
D=D-A
@XWINS
D;JEQ
@6
D=D+A
@OWINS
D;JEQ

@R3 // TERCEIRA COLUNA
D=M
@R6
D=D+M
@R9
D=D+M
@3
D=D-A
@XWINS
D;JEQ
@6
D=D+A
@OWINS
D;JEQ

@R1 // DIAGONAL PRINCIPAL
D=M
@R5
D=D+M
@R9
D=D+M
@3
D=D-A
@XWINS
D;JEQ
@6
D=D+A
@OWINS
D;JEQ

@R3 // DIAGONAL SECUNDARIA
D=M
@R5
D=D+M
@R7
D=D+M
@3
D=D-A
@XWINS
D;JEQ
@6
D=D+A
@OWINS
D;JEQ
@TURNSX
D=M
@5 // SE AS PARTIDAS ESGOTARAM, TERMINE COM EMPATE
D=D-A
@END
D;JEQ
@NEXTTURN // SE NINGUEM GANHOU, LEIA DE NOVO
0;JMP

(XWINS)
@WIN
M=1
@END
0;JMP

(OWINS)
@WIN
M=-1
@END
0;JMP

(END)
@END
0;JMP